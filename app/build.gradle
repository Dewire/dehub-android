apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'checkstyle'

apply from: "gradlescripts/checkstyle.gradle"
apply from: "gradlescripts/dependencies.gradle"

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.dewire.dehub"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-rules-libs.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules-debug.pro', 'proguard-rules-libs.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError true
        // see: https://github.com/square/okio/issues/58
        disable 'InvalidPackage'
    }
    applicationVariants.all { variant ->
        // If there is only one task (e.g. assembleDebug) we don't run our checks in order
        // not to slow down instant run. To run the checks click Run -> Clean and rerun.
        if (project.gradle.startParameter.taskNames.size() == 1) {
            return
        }
        variant.outputs.each { output ->
            def lintTask = tasks["lint${variant.name.capitalize()}"]
            def testTask = tasks["test${variant.name.capitalize()}UnitTest"]
            def checkstyle = tasks["checkstyle"]

            output.assemble.dependsOn lintTask, testTask, checkstyle
        }
    }
}

